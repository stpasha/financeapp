plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    //id 'org.springframework.cloud.contract' version '4.1.2'
}

group = 'net.microfin'
version = '0.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    //set('sentryVersion', "8.8.0")
    set('springCloudVersion', "2024.0.1")
    set('postgresqlVersion', "1.0.7.RELEASE")
    set('testContainersVersion', "1.19.0")
}

dependencies {
    //Spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation("org.springframework.data:spring-data-commons")
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    //Local
    implementation project(":chassis-api")
    implementation project(":chassis-service-client")

    //Db
    runtimeOnly 'org.postgresql:postgresql:42.7.5'
    testRuntimeOnly 'org.postgresql:postgresql:42.7.5'
    implementation("org.hibernate.orm:hibernate-core:6.6.13.Final")
    implementation("org.flywaydb:flyway-core:10.15.0")
    implementation("org.flywaydb:flyway-database-postgresql:10.15.0")
    //Util
    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.3'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave:1.4.5'  // Мост между Micrometer и Brave
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave' // Библиотека для отправки спанов в zipkin
    implementation 'io.github.openfeign:feign-micrometer:13.6'
    implementation 'org.keycloak:keycloak-admin-client:24.0.0'
    //implementation 'org.apache.kafka:kafka-log4j-appender:3.9.1'
    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'net.datafaker:datafaker:2.4.2'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
    testImplementation 'org.testcontainers:testcontainers:1.20.6'
    testImplementation 'org.testcontainers:postgresql:1.20.6'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mapstruct:mapstruct:1.6.3'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    testImplementation 'org.testng:testng:7.10.2'
    testImplementation 'org.wiremock:wiremock-standalone:3.5.4'
//    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
//    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    dependencies {
        dependency "org.apache.commons:commons-text:1.12.0"
        dependency "org.apache.httpcomponents:httpclient:4.5.14"
        dependency("commons-io:commons-io:2.18.0")
//        dependency 'org.ow2.asm:asm:9.7.1'
//        dependency 'net.java.dev.jna:jna:5.13.0'
//        dependency 'com.jcraft:jsch:0.1.05'
//        dependency 'org.apache.maven.resolver:maven-resolver-api:1.9.22'
//        dependency 'org.codehaus.plexus:plexus-utils:3.5.1'
//        dependency 'org.eclipse.sisu:org.eclipse.sisu.inject:0.9.0.M3'
//        dependency 'org.junit:junit-bom:5.11.4'
    }
}

//contracts {
//    baseClassForTests = 'net.microfin.financeapp.contract.BaseContractTest'
//}

sourceSets {
    integrationtest {
        java {
            srcDir 'src/integrationtest/java'
        }
        resources {
            srcDir 'src/integrationtest/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationtestImplementation.extendsFrom testImplementation
    integrationtestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationtest', Test) {
    description = 'Runs integration tests with TestNG'
    group = 'verification'

    testClassesDirs = sourceSets.integrationtest.output.classesDirs
    classpath = sourceSets.integrationtest.runtimeClasspath

    useTestNG()

    include '**/*IT.class'
    include '**/*IntegrationTest.class'

    shouldRunAfter tasks.test
}
tasks.named('processIntegrationtestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.check {
    dependsOn tasks.integrationtest
}

tasks.named('test') {
    useJUnitPlatform()
}

configurations.configureEach {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    resolutionStrategy {
        failOnVersionConflict()
    }
}
